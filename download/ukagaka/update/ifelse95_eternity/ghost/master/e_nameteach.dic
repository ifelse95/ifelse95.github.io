//I put anything related to who the user is in here. Name, pronouns, birthday.

OnChangeName
{
	"\0\s[0]New name?\w8 Well, \w5what should I call ya?"
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,cancelled name change]\![set,balloontimeout,-1]\![open,inputbox,OnNameTeach,-1]\e"
}

OnNameTeach
{
	_firstboot = 0; if "VeryFirstBoot" _in_ SHIORI3FW.LastTalk; _firstboot = 1 //If this is during first boot, set a temp flag
	
	_tempname = CUTSPACE(reference.raw[0]) //this stores the original name in _tempname
	_name = reference.raw[0] //putting the raw name into _name to do some tests on it
	_name = TOLOWER(_name) //changes it to lowercase
	_name = REPLACE(_name," ","") //removes spaces
	_name = REPLACE(_name,".","") //removes periods
	_name = REPLACE(_name,"-","") //removes dashes
	
	if _firstboot; "\_l[-10000,-10000]\q[ ,VeryFirstBoot]\_l[0,0]" //Prevents balloon from timing out if this is during first boot. Also prevents the user from closing out the input box
	--
	if _tempname == username && !_firstboot //If the user gives the exact same name as before. The firstboot check has to be there in case they put in 'user'
	{
		"\0\s[0]Change your mind?"
	}
	elseif "template" _in_ _name || _tempname == "Eternity" || _tempname == "eternity" //Checking with _tempname because 'X.' has a period, which would be removed. I don't want to stop someone putting in just X
	{
		"\0\s[2]Oh,\w5 that's my name! \w8\w8\0\s[16]Have a nickname?"
		--
		if !_firstboot; "\n\n\![*]\q[Cancel,OnConfigMenu,cancelled name change]\*" //If changing name through the menu
		--
		"\![open,inputbox,OnNameTeach,-1]\e"
	}
	elseif _name == "" //If the user doesn't put in anything
	{
		username = "Human"
		
		"\0\s[8]Hmmmmmmm...\w8\w8\w8\w8\n\n[half]\s[0]You're a human,\w5 right?\w8 I'll call you Human."
	}
	else //if it's none of these above names, we can use it. You can add names that just have comments and can still be used, just don't add a \e at the end.
	{
		username = _tempname
		"\0\s[12]Alright,\w5 I'll call ya %(username)!"
	}
	--
	if !_firstboot //If the user was changing their name through the config menu
	{
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	else //This is continuing from FirstBoot
	{
		"\w8\w8\s[5]Pleasure to be your friend,\w5 %(username)!! \w8\w8\s[2]Oh!\w5\s[12] How should I refer to you?\w3\n\n"
		--
		"\![*]\q[He/Him/His,OnPronouns,masc]\n/
		\![*]\q[She/Her/Hers,OnPronouns,fem]\n/	
		\![*]\q[They/Them/Their,OnPronouns,undef]\*\e"
	}
}

OnChangePronoun //Envelope names are the same as the GT template, since I figured most people will be used to them.
{
	"\0\s[6]Pronoun change?\w8 No problem.\w8\w8 What will it be?"
	--
	"\n\n\_q/
	\![*]\q[He/Him/His,OnPronouns,masc]\n/
	\![*]\q[She/Her/Hers,OnPronouns,fem]\n/	
	\![*]\q[They/Them/Their,OnPronouns,undef]\e"
}


OnPronouns
{
	if reference0 == "masc"
	{
		heshe = "he"
		himher = "him"
		hisher = "his"
		hesshes = "he's"
	}
	elseif reference0 == "fem"
	{
		heshe = "she"
		himher = "her"
		hisher = "her"
		hesshes = "she's"
	}
	else
	{
		heshe = "they"
		himher = "them"
		hisher = "their"
		hesshes = "they're"
		
	}
	--
	if "VeryFirstBoot" _in_ SHIORI3FW.LastTalk //This is the end of the firstboot dialogue
	{
		"\s[5]Coolio!\w8\s[6] For the record,\w5 I use they and them.\x\c\s[5]I hope we can be best friends, %(username)!"
		--
		"\![leave,passivemode]\e"
	}
	else //This is if the user is changing their pronouns through the menu
	{
		"\0\s[5]You got it!\w8 I'll use %(heshe) %(himher) and %(hisher) from now on!"
		--
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
}
//Pronoun examples for my own sake:
//"%(heshe) left that there."
//"%(hisher) work is really cool."
//"I'll ask %(himher) later."
//"%(hesshes) really good at it."

OnChangeBirthday //Pulled a lot of this from the GT template, but I also modified it a lot
{
	oldbirthday = birthday
	
	"\0\s[12]I'd love to know your birthday!\w5 Maybe I can throw you a party,\w5 if you want. \w8\w8\n\n[half]\0\s[0]What month were you born? \w8Write the number or name."
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,cancelled birthday month]\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\e"
}

//birthday[0] - word birthday
//birthday[1] - number birthday
//birthday[2] - word month
//birthday[3] - word day
//birthday[4] - number month
//birthday[5] - number day
//Use birthday[0] to display the user's birthday, and birthday[1] to do checks with the numbers

OnUserBornMonth
{
	_tempmonth = reference0 //this stores what the user put in as a variable called _tempmonth
	_tempmonth = TOLOWER(_tempmonth) //changes it to lowercase
	
	if _tempmonth == "january" || _tempmonth == "1" {birthday[4] = 1; birthday[2] = "January"}
	elseif _tempmonth == "february" || _tempmonth == "2" {birthday[4] = 2; birthday[2] = "February"}
	elseif _tempmonth == "march" || _tempmonth == "3" {birthday[4] = 3; birthday[2] = "March"}
	elseif _tempmonth == "april" || _tempmonth == "4" {birthday[4] = 4; birthday[2] = "April"}
	elseif _tempmonth == "may" || _tempmonth == "5" {birthday[4] = 5; birthday[2] = "May"}
	elseif _tempmonth == "june" || _tempmonth == "6" {birthday[4] = 6; birthday[2] = "June"}
	elseif _tempmonth == "july" || _tempmonth == "7" {birthday[4] = 7; birthday[2] = "July"}
	elseif _tempmonth == "august" || _tempmonth == "8" {birthday[4] = 8; birthday[2] = "August"}
	elseif _tempmonth == "september" || _tempmonth == "9" {birthday[4] = 9; birthday[2] = "September"}
	elseif _tempmonth == "october" || _tempmonth == "10" {birthday[4] = 10; birthday[2] = "October"}
	elseif _tempmonth == "november" || _tempmonth == "11" {birthday[4] = 11; birthday[2] = "November"}
	elseif _tempmonth == "december" || _tempmonth == "12" {birthday[4] = 12; birthday[2] = "December"}
	else //if the month isn't a valid month, like 32 or something.
	{
		"\0\s[3]%(playError)%(_tempmonth)...\w8 That isn't a real month?\w8 Not for humans at least..."
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu,cancelled birthday month]\![open,inputbox,OnUserBornMonth,-1]\![set,balloontimeout,-1]\*\e"
	}
	--
	//If it's a valid month
	"\s[12]%(birthday[2])!\w5 Lovely!\w8 What day?"
	--
	"\n\n\![*]\q[Cancel,OnConfigMenu,cancelled birthday day]\![open,inputbox,OnUserBornDay,-1]\![set,balloontimeout,-1]\*\e"
}

OnUserBornDay
{
	
	_tempday = reference0 //storing the input in _tempday
	_tempday = TOLOWER(_tempday) //changes it to lowercase
	_tempday = REPLACE(_tempday,"st","") //if the user types something like 2nd, 3rd, etc, this will make it just the number. It'll just add the suffix back later on the one that actually shows.
	_tempday = REPLACE(_tempday,"nd","")
	_tempday = REPLACE(_tempday,"rd","")
	_tempday = REPLACE(_tempday,"th","")
	_tempday = TOINT(_tempday) //Converts it to an integer
	_month = birthday[4] //Just for readability
	
	if ((_month == 1 || _month == 3 || _month == 5 || _month == 7 || _month == 8 || _month == 10 || _month == 12) && _tempday >= 1 && _tempday <= 31) || (_month == 2 && _tempday >= 1 && _tempday <= 29) || ((_month == 4 || _month == 6 || _month == 9 || _month == 11) && _tempday >= 1 && _tempday <= 30) //This ugly function checks if the day falls into a valid range.
	{
		birthday[5] = _tempday
		birthday[3] = Printday
		birthday[1] = "%(birthday[4])/%(birthday[5])" //If you want to write dates as DD/MM, feel free to swap this around
		birthday[0] = "%(birthday[2]) %(birthday[3])"
		ERASEVAR("oldbirthday")
		
		if birthday[1] == "%(month)/%(day)" //if today is the user's birthday. Don't forget to switch this if you change the format to DD/MM
		{
			"\0\s[2]Oh shit!\w5 Your birthday is today!\w8\0\s[5]Happy birthday, %(username)!!"
		}
		elseif birthday[1] == "12/25" //If it's on christmas. Put any date you'd like here. If you change the format to DD/MM, make sure you change any dates like this that might be in bootend.
		{
			"\0\s[8]Ooh,\w5 do you get double the presents with this birthday?!"
		}
		elseif birthday[1] == "12/21" //this is eternity's birthday
		{
			"\0\s[2]Hey!!!\w5 That's my birthday, \w5too!! \w8\w8\0\s[5]We're twins!!\w5 Haha!!"
		}
		elseif birthday[1] == "4/29" //this is my birthday.
		{
			"\0\s[8]Hmm...\w8\w8 That date sounds familiar...\w8\w8\n\n[half]\0\s[0]Kinda like I know someone else with that birthday,\w5 too?"
		}
		else //if the birthday is not listed above.
		{
			"\0\s[0]%(birthday[0]).\w8 Got it!"
		}
		--
		"\n\n\![*]\q[Back to config,OnConfigMenu]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
	else //If it isn't a valid day, we open the input box again and have a \e so the dialogue below doesn't play.
	{
		"\0\s[3]%(playError)Ehhh?\w8 Do you really have that amount of days in a month?"
		--
		"\n\n\![*]\q[Cancel,OnConfigMenu,cancelled birthday day]\![open,inputbox,OnUserBornDay,-1]\*\e"
	}
	
}

Printday //Sets the appropriate suffix
{
	if birthday[5] == 1 || birthday[5] == 21 || birthday[5] == 31
	{
		"%(birthday[5])st"
	}
	elseif birthday[5] == 2 || birthday[5] == 22
	{
		"%(birthday[5])nd"
	}
	elseif birthday[5] == 3 || birthday[5] == 23
	{
		"%(birthday[5])rd"
	}
	else
	{
		"%(birthday[5])th"
	}
}	