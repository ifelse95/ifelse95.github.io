//Todo file! Make sure to remove this dic file from your completed ghost, and remove zzz_todo from yaya.txt. Also remove the option for this menu from menu.dic!
//You don't need to touch anything in here, just use the to do list if you want it, then remove it when you're done


//---
//This is the stuff for checking if your template is up to date. I've put it here because the end user won't need this at all, and you can remove it along with the todo list when you're done. If you've put in enough work on your ghost to no longer need the to do list, you're not gonna want to switch to a new template haha
TemplateVer
{
	"1.0.9"
}

OnTemplateUpdate
{
	"\![execute,http-get,https://zichqec.github.io/s-the-skeleton/weatherregex.html,--async=OnCheckTemplateVer,--file=weatherregex.html,--timeout=300]"
}

OnCheckTemplateVer
{
    _file = "var\\weatherregex.html"
    _buff = ""
	_ver = ""
	_changelog = ""
	
    FCHARSET(1)
	--
	if FOPEN(_file,'r')
	{
		for _buff = FREAD(_file); _buff != -1; _buff = FREAD(_file) //This loop scrapes the current regex from my website
		{
			if RE_SEARCH (_buff,'YAYAVER--(.*)--YAYAVER'); {_ver = RE_GETSTR[1]}
			if RE_SEARCH (_buff,'YAYACHANGELOG--(.*)--YAYACHANGELOG'); {_changelog = RE_GETSTR[1]}
		}
	}
	--
	FCLOSE(_file);
	
	if _ver == TemplateVer
	{
		"\0\s[0]Your template is up to date!\e"
	}
	else
	{
		"\0\s[0]\b2\![set,autoscroll,disable]Your template is not up to date.\w8 The latest version is %(_ver).\w8 You can get the latest version \_a[https://zichqec.github.io/s-the-skeleton/xtemplate.html]here.\_a\w8\n\nHere's the changelog:\n\n\_q%(_changelog)\n\n\![*]\q[Back to the menu,OnMenuRun]\n\![*]\q[I'm done,cancelnodialogue]\e"
	}
}
//---


OnTemplateTodo
{
	//(Y/X) * 100 = percentage
	/*
	0 - Aitalk
	1 - Anchor
	2 - bootend
	3 - Commu
	4 - etc
	5 - menu
	6 - Mouse
	7 - Nameteach
	8 - String
	9 - Word
	10 - Misc

	11 - Surfaces
	12 - Misc shell
	13 - Balloon
	*/
	if ARRAYSIZE(TemplateTodoArray) == 0; TemplateTodoArray = ("4","1","8","1","11","7","2","3","1","1","3","6","2","5")
	
	_str = "\f[strike,1]"
	_df = "\f[default]"
	
	_totaltasks = 0
	_tasksdone = 0
	foreach TemplateTodoArray; _section
	{
		_totaltasks += TOREAL(_section[0])
		_tasksdone += (ARRAYSIZE(_section) - 1)
	}
	
	_totalpercent = 0.0
	_totalpercent = ((_tasksdone / _totaltasks) * 100)
	_totalpercent = SPLIT(_totalpercent,".")[0]
	_totaltasks = SPLIT(_totaltasks,".")[0]
	
	_menuoptions = ("Aitalk","Anchor","Bootend","Commu","Etc","Menu","Mouse","Nameteach","String","Word","Miscellaneous","Surfaces","Misc Shell","Balloon")
	
	_display = "\0\*\_q\b2\![set,autoscroll,disable]\_l[75]———Programming———\n\n[20]"
	
	_i = 0
	foreach _menuoptions; _option
	{
		if ARRAYSIZE(TemplateTodoArray[_i]) - 1 == TOINT(TemplateTodoArray[_i][0]); _display += "%(_str)"
		_display += "\q[%(_option),OnTemplateTodoSub,%(_i),%(_option)]%(_df)\_l[100](%(ARRAYSIZE(TemplateTodoArray[_i]) - 1)/%(TemplateTodoArray[_i][0]))\n"
		_i++
		if _i == 11; _display += "\_l[105]———Art———\n\n[20]"
	}
	--
	"%(_display)"
	--
	"\n[20]\_l[52]%(_tasksdone)/%(_totaltasks) completed, %(_totalpercent)%"
	--
	"\n\n\![*]\q[Back to the Menu,OnMenuRun]  \![*]\q[Close,cancelnodialogue]\e"
}

OnTemplateTodoSub
{
	_str = "\f[strike,1]"
	_df = "\f[default]"
	
	_tasksdone = (ARRAYSIZE(TemplateTodoArray[reference0]) - 1)
	_totaltasks = TOREAL(TemplateTodoArray[reference0][0])
	
	_totalpercent = 0.0
	_totalpercent = ((_tasksdone / _totaltasks) * 100)
	_totalpercent = SPLIT(_totalpercent,".")[0]
	_totaltasks = SPLIT(_totaltasks,".")[0]
	
	_menuoptions = IARRAY
	_display = "\0\*\_q\b2———%(reference1)———\n\n"
	--
	if reference0 == "0"; _menuoptions = ("Random Dialogue","Overlap","Offscreen","Top of the Hour")
	elseif reference0 == "1"; "\![raise,OnTemplateTodoBasic,1,Anchor]\e"
	elseif reference0 == "2"; _menuoptions = ("First Boot","Booting Dialogue","Closing Dialogue","Minimize","Switching to/from another ghost","Reloading","Shell Changing","Dressup on/off")
	elseif reference0 == "3"; "\![raise,OnTemplateTodoBasic,3,Commu]\e"
	elseif reference0 == "4"; _menuoptions = ("Drag and Drop","Installing","Uninstalling","Creating Files","Email Checking","Updating","Headlines/RSS","SNTP","Screensaver","Battery Low","Shell Scaling")
	elseif reference0 == "5"; _menuoptions = ("Open Calendar","SSP Preferences","Recycle Bin","Bug Report","Config Menu","Talk Rate Changing","Cancel and Timeout Dialogue")
	elseif reference0 == "6"; _menuoptions = ("Petting","Punching")
	elseif reference0 == "7"; _menuoptions = ("Name Teaching","Pronoun Teaching","Birthday Teaching")
	elseif reference0 == "8"; "\![raise,OnTemplateTodoBasic,8,String]\e"
	elseif reference0 == "9"; "\![raise,OnTemplateTodoBasic,9,Word]\e"
	elseif reference0 == "10"; _menuoptions = ("install.txt","descript.txt","yaya.txt")
	elseif reference0 == "11"; _menuoptions = ("Draw Surfaces","Pick Number Ranges","Set up collisions","Set up blink frames","Set up talk frames","Set up a surfacetable")
	elseif reference0 == "12"; _menuoptions = ("Tray Icon","Right Click Menu BG")
	elseif reference0 == "13"; _menuoptions = ("Balloon Base","Up and Down arrows","Choice Marker","Online Marker","Put all the details together")
	
	foreach _menuoptions; _option
	{
		if _option _in_ TemplateTodoArray[reference0]; _display += "%(_str)"
		_display += "\q[%(_option),OnTemplateTodoCheck,%(reference0),%(reference1),%(_option)]%(_df)\n"
	}
	--
	"%(_display)"
	--
	"\n\n%(_tasksdone)/%(_totaltasks) completed, %(_totalpercent)%"
	--
	"\n\n\![*]\q[Back to todo menu,OnTemplateTodo]\n\![*]\q[Close,cancelnodialogue]"
}

OnTemplateTodoCheck
{
	_toedit = TemplateTodoArray[reference0]
	if reference2 _in_ _toedit; _toedit = REPLACE(_toedit,",%(reference2)","")
	else; _toedit += ",%(reference2)"
	TemplateTodoArray[reference0] = _toedit
	--
	"\![raise,OnTemplateTodoSub,%(reference0),%(reference1)]"
}

OnTemplateTodoBasic
{
	if ARRAYSIZE(TemplateTodoArray[reference0]) > 1; TemplateTodoArray[reference0] = TemplateTodoArray[reference0][0]
	else; TemplateTodoArray[reference0] += ",%(reference1)"
	--
	"\![raise,OnTemplateTodo,%(reference0)]"
}